# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=libhandy
pkgver=1.0.0
pkgrel=1
pkgdesc="Library full of GTK+ widgets for mobile phones"
arch=(x86_64)
url="https://source.puri.sm/Librem5/libhandy"
license=(LGPL)
depends=(glib2 gtk3)
makedepends=(git glade gobject-introspection meson vala)
checkdepends=(xorg-server-xvfb)
provides=(libhandy-1.so)
_commit=94313c206258860b2428712e7ece1d02c5177857  # tags/1.0.0^0
source=("git+https://gitlab.gnome.org/GNOME/libhandy.git#commit=$_commit")
sha256sums=('SKIP')

prepare() {
  cd $pkgname

  # Fix build with glade 3.38.0
  git cherry-pick -n 65ff089b7f346f3809e9e0050ea32ac003465ede
}

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  arch-meson $pkgname build \
    -D gtk_doc=true \
    -D examples=false
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run \
    -s '-screen 0 1920x1080x24 -nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
