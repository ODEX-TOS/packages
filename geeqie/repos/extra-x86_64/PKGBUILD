# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=geeqie
pkgver=1.5.1
pkgrel=3
pkgdesc='Lightweight image viewer'
arch=('x86_64')
url="http://www.geeqie.org/"
license=('GPL2')
depends=('exiv2' 'gtk2' 'ffmpegthumbnailer')
makedepends=('intltool' 'doxygen' 'yelp-tools' 'python')
optdepends=('librsvg: SVG rendering'
            'fbida: for jpeg rotation')
validpgpkeys=('85D4CA42952C949B175362B379D0B06F4E20AF1C')
source=("http://www.geeqie.org/${pkgname}-${pkgver}.tar.xz"{,.asc}
        'gcc10.patch'
        'yelp-build.patch'
        'geeqie-no-changelog.patch')
sha256sums=('4854d5d323c31f8f4068fd73ab2c454ff91e826c4ca4d37b22c246ad14dea10a'
            'SKIP'
            '9589e10d88e3e8d9d0ada45d595fc95b75eead6a99b2e5932955198f74368e0a'
            '511d12cbf24ddc5fa8bea0809dc0fbe9587555545d1edd88034a4a72bd972d67'
            'f6eb2725a47fc750d95455d4bbf4be5a0ae45307d3ada571937212a25b2b4301')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i ../gcc10.patch
	patch -p1 -i ../yelp-build.patch
	patch -p1 -i ../geeqie-no-changelog.patch # Workaround missing changelog (Fedora)
	NOCONFIGURE=1 ./autogen.sh
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr
	make
}

package(){
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
