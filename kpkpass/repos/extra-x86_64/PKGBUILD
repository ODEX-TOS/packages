# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kpkpass
pkgver=20.12.0
pkgrel=1
pkgdesc="Apple Wallet Pass reader"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(karchive)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('a337c2e374d1b2e820b921911484425aabd4fe864fd5c8103931b7294d3617ec'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
