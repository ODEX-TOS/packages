# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=falkon
pkgver=3.1.0
pkgrel=12
pkgdesc="Cross-platform QtWebEngine browser"
arch=(x86_64)
url="https://www.falkon.org/"
license=(GPL3)
depends=(qt5-webengine qt5-x11extras qt5-svg)
makedepends=(extra-cmake-modules qt5-tools kio purpose libgnome-keyring pyside2 shiboken2)
optdepends=('kio: KDE integration' 'purpose: KDE integration' 'libgnome-keyring: gnome keyring integration'
            'pyside2: Python plugins')
replaces=(qupzilla)
source=(https://download.kde.org/stable/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig}
        falkon-pyside-5.12.2.patch::"https://invent.kde.org/network/falkon/-/commit/479933e0.patch"
        falkon-qt-5.14.patch::"https://invent.kde.org/network/falkon/-/commit/bbde5c69.patch"
        falkon-qt-5.15.patch::"https://invent.kde.org/network/falkon/-/commit/2ca83509.patch")
sha256sums=('ce743cd80c0e2d525a784e29c9b487f73480119b0567f9ce8ef1f44cca527587'
            'SKIP'
            '7e104c6d8d18f3923195eef7f77808221300eff52f88f76306bc51f20a7c84a7'
            'b3d5eed42a511b458a3d4a9fe1d4f4b9e93358668fdc79c1807b759869c640a0'
            '69c5319198fc8e4a58f7e867aedf182c57e4a08e95505a1b06a8e3cd18ac4534')
validpgpkeys=(3CB7B8416EF6778B409FDC43EBC3FC294452C6D8) # David Rosca <nowrep@gmail.com>

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../falkon-pyside-5.12.2.patch # Fix build with PySide 5.12.2
  patch -p1 -i ../falkon-qt-5.14.patch # Fix build with Qt 5.14
  patch -p1 -i ../falkon-qt-5.15.patch # Fix build with Qt 5.15
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
