# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=asio
pkgver=1.16.1
pkgrel=1
pkgdesc='Cross-platform C++ library for ASynchronous network I/O'
url='https://think-async.com/Asio/'
arch=('x86_64')
license=('custom:boost')
makedepends=('boost')
source=(https://sourceforge.net/projects/asio/files/asio/${pkgver}%20%28Stable%29/asio-${pkgver}.tar.bz2)
sha256sums=('e271db76dbbcda9835ed1c9c94deb2ba3f4589c3ebcaa71d99ac694b8d62638c')
b2sums=('810fd735a0cbea8e07bfe30cbf02d44469c3c7eebed3822766c0a658fe8972681232afe4a6124537886fafd0769eecc40b8eabbcaa2771b84b93cda56329394d')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -Dm 644 COPYING LICENSE_1_0.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
