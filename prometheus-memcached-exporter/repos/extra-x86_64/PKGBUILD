# Maintainer: Jelle van der Waa <jelle@dwaa.nl>

pkgname=prometheus-memcached-exporter
pkgver=0.7.0
pkgrel=2
pkgdesc="Exports metrics from memcached servers for consumption by Prometheus"
arch=('x86_64')
url="https://github.com/prometheus/memcached_exporter"
license=('Apache')
depends=(glibc memcached)
makedepends=(go)
source=($pkgname-$pkgver.tar.gz::https://github.com/prometheus/memcached_exporter/archive/v${pkgver}.tar.gz prometheus-memcached-exporter.service
        prometheus-memcached-exporter.conf)
sha512sums=('5df59a73142e12c86bdc2da49627128c49a22a8bd380d3e44c091cb54c6d0f78d51fd3f95b884bd0260537f87b68989bfd24838abac21805c1b56506ef1bea82'
            '7af6ef9df784e97ecc08e77ee473b4b7860eb8a82c5af2a8cf1eec1850e71750e6d71bed1fa24744dc57c6b1dd6da31da1ec233056f29f9e58e64d5541af7280'
            'bc92af08207bee78a48cc764f038d6f5175b0051873f1238d90531f81d9b403085ec7ac62dfe934ee804794426c6bed4c309b374a418c9f7a4dbdbb157d58c23')

check() {
  cd memcached_exporter-$pkgver
  memcached_pids=()
  memcached -p 11211 -U 11211 >/dev/null        & memcached_pids+=($!)

  local ret=0
  go test ./... || ret=1

  kill ${memcached_pids[@]}

  return $ret
}

build() {
  cd memcached_exporter-$pkgver
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-extldflags ${LDFLAGS} \
      -X github.com/prometheus/common/version.Version=$pkgver \
      -X github.com/prometheus/common/version.Revision=$pkgver \
      -X github.com/prometheus/common/version.Branch=tarball \
      -X github.com/prometheus/common/version.BuildUser=someone@builder \
      -X github.com/prometheus/common/version.BuildDate=$(date -d@"$SOURCE_DATE_EPOCH" +%Y%m%d-%H:%M:%S)" \
    .
}

package() {
  install -Dm644 prometheus-memcached-exporter.service "$pkgdir"/usr/lib/systemd/system/prometheus-memcached-exporter.service
  install -Dm644 prometheus-memcached-exporter.conf "${pkgdir}"/etc/conf.d/prometheus-memcached-exporter

  cd memcached_exporter-$pkgver
  install -Dm755 memcached_exporter "$pkgdir"/usr/bin/prometheus-memcached-exporter
}
