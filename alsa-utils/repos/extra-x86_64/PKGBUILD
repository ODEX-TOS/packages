# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=alsa-utils
pkgver=1.2.3
pkgrel=2
pkgdesc="Advanced Linux Sound Architecture - Utilities"
arch=('x86_64')
url="https://www.alsa-project.org"
license=('GPL2')
depends=('glibc' 'pciutils' 'psmisc')
makedepends=('alsa-lib' 'docbook-xsl' 'fftw' 'libsamplerate' 'ncurses'
'python-docutils' 'systemd' 'xmlto')
optdepends=('fftw: for alsabat')
# we require /var/lib/alsa for state file
options=(emptydirs)
source=("https://www.alsa-project.org/files/pub/utils/$pkgname-$pkgver.tar.bz2")
sha512sums=('5aeba2e5b7279c62d4df85b61a26b3f41e5465bac5a0b94c7bbe1299f7aa38e8cf2a1f749f618289a63b61f76609fe54169fcbe70792b45adc8957811b672012')
b2sums=('70037123de16d4d267fbe862acba31144e1f060cb01c294cf0691d9378e1f04b108c9f3a5e071979abe9b5c1c15a49facc9e0be73122caaf812047a514e4de4d')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --disable-alsaconf \
              --sbindir=/usr/bin \
              --with-udev-rules-dir=/usr/lib/udev/rules.d \
              --with-systemdsystemunitdir=/usr/lib/systemd/system
  make
}

package() {
  depends+=('libasound.so' 'libformw.so' 'libmenuw.so' 'libncursesw.so'
  'libpanelw.so' 'libsamplerate.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  # dir where to save ALSA state
  install -d "${pkgdir}/var/lib/alsa"
}
