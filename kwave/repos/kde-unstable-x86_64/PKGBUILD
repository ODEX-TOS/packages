# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Contributor: Elvis Angelaccio <elvis.angelaccio@kdemail.net>
# Contributor: Gustavo alvarez <sl1pkn07@gmail.com>

pkgname=kwave
pkgver=20.07.80
pkgrel=1
pkgdesc="A sound editor for KDE"
arch=(x86_64)
url="https://kwave.sourceforge.net"
license=(GPL)
depends=(kio audiofile libmad libsamplerate hicolor-icon-theme id3lib fftw)
makedepends=(extra-cmake-modules kdoctools doxygen librsvg opus)
optdepends=('opus: OGG plugin')
groups=(kde-applications kde-multimedia)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('e2d46889d0e07f843191846cc90e5cdb4ee3ad94e737855fa7b297df5999937b'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
