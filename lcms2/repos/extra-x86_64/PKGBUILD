# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=lcms2
pkgver=2.11
pkgrel=1
pkgdesc='Small-footprint color management engine, version 2'
arch=('x86_64')
url='http://www.littlecms.com/'
license=('MIT')
depends=('libtiff')
provides=('liblcms2.so')
source=("https://github.com/mm2/Little-CMS/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('8aa7880db720f75d14edc4f3318e2a8843453bf2a36f42d04671cef1719104a6')

build() {
  cd Little-CMS-${pkgver}

  ./configure \
    --prefix=/usr
  make
}

check() {
  cd Little-CMS-${pkgver}

  make check
}

package() {
  cd Little-CMS-${pkgver}

  make DESTDIR="${pkgdir}" install
  install -D -m0644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
