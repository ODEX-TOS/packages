# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>

pkgname=libcap-ng
pkgver=0.7.11
pkgrel=2
pkgdesc='Library making programming with POSIX capabilities easier than traditional libcap'
url='https://people.redhat.com/sgrubb/libcap-ng/'
arch=('x86_64')
license=('LGPL2.1')
depends=('glibc')
provides=('libcap-ng.so')
source=(https://github.com/stevegrubb/libcap-ng/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('ad2f07df90b730831fda87f2f8ad889602ac627d63dbdb5db8a39af6ad65a0b3e08eb37ea99cf5604a16f0b526ae3f847e03493663f66178590a8c6dfadae4eb')
b2sums=('f634dceef47840f573b8c84d2af9f511486ea6d23a0e41d1157f1d0a433150aeba08da75284237573157ca9d5429cb47a79cfbe40356a7f875c8a14184173e15')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --enable-static=no \
    -with-python=no
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
