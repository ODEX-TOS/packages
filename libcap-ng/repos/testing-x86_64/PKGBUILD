# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>

pkgname=libcap-ng
pkgver=0.8
pkgrel=1
pkgdesc='Library making programming with POSIX capabilities easier than traditional libcap'
url='https://people.redhat.com/sgrubb/libcap-ng/'
arch=('x86_64')
license=('LGPL2.1')
depends=('glibc')
provides=('libcap-ng.so')
source=(https://github.com/stevegrubb/libcap-ng/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('47b079849ee051544e74b006bcfff1f7965c79099db52f39f49e3f138137748712df2ef88f3fbd9715d44d9f2d118593f557dba1ede45a0168e7c813a4e65f46')
b2sums=('e31be8b3f3879f5a266c9ca2893353c84b628833d923992f3aaedfca5ca60b7a5d3beceb4144911ebc311c5cbb7bcf442e5221aa2bbf494d5eac9ef11d253909')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --enable-static=no \
    -with-python=no
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
