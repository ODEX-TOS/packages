# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kpatience
pkgver=20.04.3
pkgrel=1
pkgdesc="Offers a selection of solitaire card games"
url="https://kde.org/applications/games/kpatience/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kde-games)
depends=(libkdegames freecell-solver black-hole-solver hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/stable/release-service/$pkgver/src/kpat-$pkgver.tar.xz"{,.sig})
sha256sums=('2a207c9efa1a62c99f8b33cf44782c563ee032b706ef63548239a4c303ec9a66'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S kpat-$pkgver \
    -DBUILD_TESTING=OFF \
    -DWITH_BH_SOLVER=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
