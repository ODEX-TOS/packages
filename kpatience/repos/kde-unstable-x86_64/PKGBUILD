# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kpatience
pkgver=20.07.80
pkgrel=1
pkgdesc="Offers a selection of solitaire card games"
url="https://kde.org/applications/games/kpatience/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kde-games)
depends=(libkdegames freecell-solver black-hole-solver hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/kpat-$pkgver.tar.xz"{,.sig}
         kpatience-fix-build.patch::"https://invent.kde.org/games/kpat/-/commit/29c4b431.patch")
sha256sums=('5525b0dc724250eaded10ac21898889e4f2bf8b997a89b73121147aa16c5d106'
            'SKIP'
            '1fbdde3e02aec4efd9b41eaddd4f38964706585b7c43b5cf1c2c156151e9ec9b')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  patch -d kpat-$pkgver -p1 -i ../kpatience-fix-build.patch
}

build() {
  cmake -B build -S kpat-$pkgver \
    -DBUILD_TESTING=OFF \
    -DWITH_BH_SOLVER=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
