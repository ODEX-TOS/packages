# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kcalutils
pkgver=20.04.2
pkgrel=1
pkgdesc="The KDE calendar utility library"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(kcalendarcore kidentitymanagement)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('c5ebbb49f7caf16f613bf0c41191caaf04757b648ce7fa3f93bc08882e9d4315'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
