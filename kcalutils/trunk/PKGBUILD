# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kcalutils
pkgver=20.04.3
pkgrel=1
pkgdesc="The KDE calendar utility library"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(kcalendarcore kidentitymanagement)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('6604e0377c8955be7963853691aeaf689c00e2f78caab6b3472ffb4c822e07a3'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
