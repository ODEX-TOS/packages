# Maintainer:
# Contributor: Zachary Vance <za3k at za3k . com>
 
pkgname=paper
pkgver=2.1
pkgrel=1
arch=(x86_64)
pkgdesc="Replacement for Debian's libpaper"
url="https://github.com/rrthomas/paper"
license=(GPL3)
depends=(perl)
makedepends=(help2man)
replaces=(rrthomas-libpaper)
source=($pkgname-$pkgver.tar.gz::"https://github.com/rrthomas/paper/archive/v$pkgver.tar.gz"
        gnulib.tar.gz::"http://git.savannah.gnu.org/gitweb/?p=gnulib.git;a=snapshot;h=d279bc6d9f9323e19ad8c32b6d12ff96dfb0f5ba;sf=tgz")
sha512sums=('492cc0312eedf47d0bf8c8d7233f908a47c024233f00cb248b6ce1abff5f973acaf1fa9ae3287d5c5344e73b5f1e7c599ddca4ebfe3c9ebb2a75b80e7f369406'
            'b3197bd8b90ac0650d24eed4a337ddd710d2e0b969523426d94bef29d9d7c67d8cb242d9e5db1b018af49bb7ffb362c99afa6c9c60d9a4b28d4dc3e2b665b5cb')

build() {
  cd $pkgname-$pkgver
  
  ./bootstrap --skip-git --gnulib-srcdir="$srcdir"/gnulib-d279bc6
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
