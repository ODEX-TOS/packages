# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kblog
pkgver=20.04.2
pkgrel=1
pkgdesc="A blogging library for KDE"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(kxmlrpcclient syndication kcalendarcore)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('345fa87ddeffd237d62d16c2fbb4974883e3592e368bb087393dbf1af7c11af1'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
