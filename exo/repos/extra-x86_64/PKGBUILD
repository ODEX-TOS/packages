# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=exo
pkgver=0.12.11
pkgrel=5
pkgdesc="Application library for Xfce"
arch=('x86_64')
url="https://www.xfce.org/"
license=('GPL2' 'LGPL')
groups=('xfce4')
depends=('libxfce4ui' 'perl-uri' 'hicolor-icon-theme')
makedepends=('intltool')
source=(https://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        fix-xfce4-settings-manager-resizing.patch)
sha256sums=('ec892519c08a67f3e0a1f0f8d43446e26871183e5aa6be7f82e214f388d1e5b6'
            '0bb6896fae60b06ba4accd15cd334da88f874849f6fabd39ef5831f977c8a181')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # https://gitlab.xfce.org/xfce/exo/-/issues/19
  patch -Np1 -i ../fix-xfce4-settings-manager-resizing.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
