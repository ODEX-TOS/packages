# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=dav1d
pkgver=0.7.1
pkgrel=1
pkgdesc='AV1 cross-platform decoder focused on speed and correctness'
url='https://code.videolan.org/videolan/dav1d/'
arch=('x86_64')
license=('BSD')
depends=('glibc' 'vulkan-icd-loader')
makedepends=('meson' 'ninja' 'nasm' 'doxygen' 'vulkan-headers')
provides=('libdav1d.so')
source=(https://downloads.videolan.org/pub/videolan/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz{,.asc})
sha512sums=('fe7f4a4a547d1239e62025bb40d2f7f97e9fbdfde1d32f9930497801b703a68050ee2fa79793c1cd0c4723678a0736f32431e1b711f63f5de782fe675e5c82de'
            'SKIP')
b2sums=('9f69741cf87c7f4f0044890514fdf8261936f6d65653ccbd5669cbbbc1cff3dcee165b96da6dac39000e790d003062368967b1282d5f0955fdaeef5a6e2905ca'
        'SKIP')
validpgpkeys=('65F7C6B4206BD057A7EB73787180713BE58D1ADC') # VideoLAN Release Signing Key

build() {
  cd ${pkgbase}-${pkgver}
  arch-meson build
  ninja -C build
}

check() {
  cd ${pkgbase}-${pkgver}/build
  meson test
}

package() {
  cd ${pkgbase}-${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
