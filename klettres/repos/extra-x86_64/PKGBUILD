# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=klettres
pkgver=20.08.1
pkgrel=1
pkgdesc="Learn The Alphabet"
url="https://kde.org/applications/education/klettres/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kde-education)
depends=(knewstuff phonon-qt5 hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('f29d6df1de3562731f246a892caaa493b9bccc9d1317a948e03ad12cb8c80c06'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
