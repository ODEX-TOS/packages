# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Ronald van Haren <ronald@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Jason Taylor <jftaylor21@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>

pkgname=qhull
pkgver=2020.1
_pkgver=8.0.0
pkgrel=1
pkgdesc="A general dimension code for computing convex hulls and related structures"
arch=(x86_64)
url="http://www.qhull.org/"
license=(custom)
depends=(glibc)
makedepends=(cmake)
source=("http://www.qhull.org/download/qhull-${pkgver%.*}-src-$_pkgver.tgz"
         qhull-no-static.patch)
sha256sums=('1ac92a5538f61e297c72aebe4d4ffd731ceb3e6045d6d15faf1c212713798df4'
            '8c9e7bda62da8f9c02429b0839610adf6e22525e0de6eb6df2a95cf3dad8b7ab')

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../qhull-no-static.patch # Disable static libraries
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() { 
  DESTDIR="$pkgdir" cmake --install build

  # Install license
  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
