# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias [tobias at archlinux.org]

pkgname=mousepad
pkgver=0.5.0
pkgrel=1
pkgdesc="Simple text editor for Xfce"
arch=('x86_64')
url="https://www.xfce.org/"
license=('GPL2')
groups=('xfce4-goodies')
depends=('xfconf' 'gtksourceview3' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool' 'perl')
source=(https://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('b31b24cd489467c497c25c1cfe6b32ba941b312054eace77bcaa9e429a373912')

prepare() {
  mv mousepad-0.5.{,0}
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
