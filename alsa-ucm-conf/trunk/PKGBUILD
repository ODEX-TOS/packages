# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=alsa-ucm-conf
pkgver=1.2.3
pkgrel=1
pkgdesc="ALSA Use Case Manager configuration (and topologies)"
url="https://alsa-project.org/"
arch=('any')
license=('BSD')
source=("https://www.alsa-project.org/files/pub/lib/$pkgname-$pkgver.tar.bz2")
sha512sums=('1bdb024170f1dc18170c1d3699798c14d9cc7bbaccfe6eaef1fac0ec8c28074d08ed722bf47996682d4a0737138112b7eb4d45e224f00b027b3331da728e47cd')
b2sums=('d280ebb530c58df706d9b01c169472af8a88ec87735a8c5639981a2fddee31add0d78a2e47b43abf5bd13ef051dbb25749c701174aa0ea1b7255f771856e3e12')

package() {
  cd "${pkgname}-${pkgver}"
  for dir in ucm2/*; do
    if [ -d "$dir" ] && [[ "$dir" == */codecs ]] || [[ "$dir" == */dsps ]] || [[ "$dir" == */platforms ]]; then
      for subdir in "${dir}/"*; do
        install -vDm 644 "${subdir}"/*.conf -t "$pkgdir/usr/share/alsa/ucm2/$(basename $dir)/$(basename $subdir)"
      done
    else
      [ -d "$dir" ] && install -vDm 644 "${dir}"/*.conf -t "$pkgdir/usr/share/alsa/ucm2/$(basename $dir)"
    fi
  done
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -vDm 644 ucm2/README.md -t "$pkgdir/usr/share/doc/$pkgname/ucm2"
}
