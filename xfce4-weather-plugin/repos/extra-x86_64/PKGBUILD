# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Suzy Williams <suzanne.williams3@verizon.net>

pkgname=xfce4-weather-plugin
pkgver=0.10.1
pkgrel=2
pkgdesc="A weather plugin for the Xfce4 panel"
arch=('x86_64')
url="https://docs.xfce.org/panel-plugins/xfce4-weather-plugin"
license=('GPL2')
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libxml2' 'libsoup' 'hicolor-icon-theme')
makedepends=('intltool')
source=(https://archive.xfce.org/src/panel-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        improve-contrast-in-weather-report.patch)
sha256sums=('afb2af5f3effc2ea6181636ed0e82e6dafd556ec1b8478100802f85a5d167a89'
            'da52ea354f740b3543ca74eba41cd5c279a2de0a22dbf8526805839cdc5c75db')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i ../improve-contrast-in-weather-report.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug \
    --disable-upower
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
