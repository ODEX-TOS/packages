# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: tobias [tobias [at] archlinux.org]

pkgname=mutt
pkgver=1.14.7
pkgrel=1
pkgdesc='Small but very powerful text-based mail client'
url='http://www.mutt.org/'
license=('GPL2')
arch=('x86_64')
depends=('gdbm' 'glibc' 'libgpg-error' 'openssl' 'libsasl' 'mime-types' 'krb5'
'sqlite' 'zlib')
makedepends=('gpgme' 'libidn2' 'ncurses')
optdepends=('perl: for smime_keys'
            'smtp-forwarder: to send mail')
backup=('etc/Muttrc')
source=("https://bitbucket.org/mutt/mutt/downloads/${pkgname}-${pkgver}.tar.gz"{,.asc})
sha512sums=('dc9739b5f0a99ca70fcbd495c71fbead23e3481f9c9e426feb827997c9c42e5f28355084f54788820c96a079dedb649fcc20e69436fb3c4df7e46f372b533e7c'
            'SKIP')
b2sums=('f729412d3da97fe9bd76733705317660edfa3858a9eb1012179fb289b03da58d79b6e261dbdc4ab8b001e89ca455e9972bab8f270bac28e887afc7e0bd9fc8c1'
        'SKIP')
validpgpkeys=('8975A9B33AA37910385C5308ADEF768480316BDA') # Kevin J. McCarthy <kevin@8t8.us>

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --enable-debug \
              --enable-gpgme \
              --enable-pop \
              --enable-imap \
              --enable-smtp \
              --enable-hcache \
              --enable-sidebar \
              --enable-autocrypt \
              --with-curses=/usr \
              --with-gss=/usr \
              --with-ssl=/usr \
              --with-sqlite3 \
              --with-sasl \
              --with-idn2
  make
}

package() {
  depends+=('libgpgme.so' 'libidn2.so' 'libncursesw.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # /etc/mime.types is provided by mailcap
  rm "${pkgdir}"/etc/mime.types{,.dist}
  install -vDm 644 contrib/gpg.rc "${pkgdir}"/etc/Muttrc.gpg.dist
}
