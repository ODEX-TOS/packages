# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=openjpeg2
pkgver=2.3.1
pkgrel=3
pkgdesc="An open source JPEG 2000 codec, version ${pkgver}"
arch=(x86_64)
license=('custom: BSD')
url="https://github.com/uclouvain/openjpeg"
makedepends=('cmake' 'doxygen')
depends=('zlib' 'libpng' 'libtiff' 'lcms2')
source=("$pkgname-$pkgver.tar.gz::https://github.com/uclouvain/openjpeg/archive/v$pkgver.tar.gz"
        CVE-2020-27823.patch::https://github.com/uclouvain/openjpeg/commit/b2072402b7e14d22bba6fb8cde2a1e9996e9a919.patch)
sha256sums=('63f5a4713ecafc86de51bfad89cc07bb788e9bba24ebbf0c4ca637621aadb6a9'
            'SKIP')

prepare() {
  cd "${srcdir}"
  mkdir build

  # Install doxygen docs to the right directory
  sed -i -e "s:DESTINATION\ share/doc:DESTINATION\ share/doc/${pkgname}:" openjpeg-${pkgver}/doc/CMakeLists.txt

  # https://bugs.archlinux.org/task/68906
  pushd openjpeg-${pkgver}
  patch -Np1 -i ../CVE-2020-27823.patch
  popd
}

build() {
  cd "${srcdir}/build"

  cmake "../openjpeg-${pkgver}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_STATIC_LIBS=OFF \
    -DBUILD_DOC=on

  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}/usr/share/licenses/openjpeg2"
  mv "${pkgdir}"/usr/share/doc/openjpeg-*/LICENSE "${pkgdir}"/usr/share/licenses/openjpeg2
  rmdir "${pkgdir}"/usr/share/doc/openjpeg-*
}
