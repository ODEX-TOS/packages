# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kiten
pkgver=20.08.1
pkgrel=2
pkgdesc="Japanese Reference/Study Tool"
url="https://kde.org/applications/education/kiten/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kde-education)
depends=(kio hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        kiten-crash-sessionconfig.patch::"https://invent.kde.org/education/kiten/-/commit/0f2de843.patch")
sha256sums=('f3764d090db8027746e83c326e833680fd669dca66dd6af095ba120e66de2901'
            'SKIP'
            'd6ddcfe2c67d54e6a0656040fb1d9ff3403c0086675fee4dbce37b0082071f0c')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../kiten-crash-sessionconfig.patch # Fix crash
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
