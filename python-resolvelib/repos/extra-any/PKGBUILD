# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-resolvelib
pkgname=(python-resolvelib python2-resolvelib)
pkgver=0.5.3
pkgrel=1
pkgdesc="Resolve abstract dependencies into concrete ones"
url="https://github.com/sarugaku/resolvelib"
license=('ISC')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-commentjson' 'python-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/sarugaku/resolvelib/archive/$pkgver.tar.gz")
sha512sums=('dc25d890ae1475572c32c40b167a292fc0a9a34a6be0cbf31c70499048f91b05ecc889a427f2bb8c43054227efcccae03d38e3c00ee8c885b338ce00105eb65d')

build() {
  cd resolvelib-$pkgver
  python setup.py build
  python2 setup.py build
}

check() {
  cd resolvelib-$pkgver
  python setup.py pytest
}

package_python-resolvelib() {
  depends=('python')

  cd resolvelib-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

package_python2-resolvelib() {
  depends=('python2')

  cd resolvelib-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
