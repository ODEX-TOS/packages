# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kmouth
pkgver=20.07.80
pkgrel=1
arch=(x86_64)
pkgdesc='Speech Synthesizer Frontend'
url='https://kde.org/applications/utilities/kmouth/'
license=(GPL FDL)
groups=(kde-applications kde-accessibility)
depends=(kio hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('0e12c74f6a28995207a959ca3b3fa920360e3c855c86124f25413f9c55930116'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
