# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=composer
pkgver=1.10.10
pkgrel=1
pkgdesc="Dependency Manager for PHP"
arch=('any')
license=('MIT')
url='https://getcomposer.org/'
depends=('php')
makedepends=('git' 'unzip')
checkdepends=('mercurial')
replaces=('php-composer')
conflicts=('php-composer')
provides=('php-composer')
source=("${pkgname}::git+https://github.com/composer/composer.git#tag=${pkgver}"
        "${pkgname}-${pkgver}.phar::https://getcomposer.org/download/${pkgver}/composer.phar")
sha512sums=('SKIP'
            'd9a2478382d7ed4502593860c958c44327928c989bbf7be363e916d9fcc2e9ad95e041e7845331f096017c1e88f184bbaa9a635c670ca5cbb075736bf297b640')

prepare() {
	cd "${srcdir}/${pkgname}"

	php "${srcdir}/${pkgname}-${pkgver}.phar" -n install --no-dev --prefer-dist
}

build() {
	cd "${srcdir}/${pkgname}"

	php -d phar.readonly=Off bin/compile
}

check() {
	cd "${srcdir}/${pkgname}"

	# check if the upstream build and ours differ
	diff -q ${srcdir}/${pkgname}-${pkgver}.phar composer.phar

	bin/composer validate

	git config --global user.email "${USER}@${HOST}"
	git config --global user.name "${USER}"

	# Install the dev dependencies to run the unit tests
	bin/composer -n install --prefer-dist

	# Fix test dependencies for Composer 1.10.7
	bin/composer require --dev 'symfony/phpunit-bridge:^4.2'

	SYMFONY_PHPUNIT_VERSION=7.5 vendor/bin/simple-phpunit
}

package() {
	cd "${srcdir}/${pkgname}"

	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm755 composer.phar "${pkgdir}/usr/bin/composer"
}
