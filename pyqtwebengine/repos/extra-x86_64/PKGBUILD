# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=pyqtwebengine
pkgname=(python-pyqtwebengine)
pkgdesc="Python bindings for QtWebEngine"
pkgver=5.15.1
pkgrel=2
arch=(x86_64)
url="https://www.riverbankcomputing.com/software/pyqtwebengine/intro"
license=(GPL)
groups=(pyqt5)
depends=(python-pyqt5 qt5-webengine)
makedepends=(sip5 pyqt-builder)
conflicts=(pyqtwebengine-common)
source=("https://pypi.python.org/packages/source/P/PyQtWebEngine/PyQtWebEngine-$pkgver.tar.gz")
sha256sums=('f0ca7915ee206ba5d703168c6ca40b0aad62c67360328fae4af5359cdbcee439')

build() {
  cd PyQtWebEngine-$pkgver
  sip-build \
    --no-make \
    --api-dir /usr/share/qt/qsci/api/python
  cd build
  make
}

package_python-pyqtwebengine() {
  cd PyQtWebEngine-$pkgver/build
  make INSTALL_ROOT="$pkgdir" install -j1
}
