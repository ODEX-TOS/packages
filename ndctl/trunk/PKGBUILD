# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ndctl
pkgver=69
pkgrel=1
pkgdesc='Utility library for managing the libnvdimm (non-volatile memory device) sub-system in the Linux kernel'
arch=('x86_64')
url="https://github.com/pmem/ndctl"
license=('LGPL')
depends=('json-c' 'keyutils' 'kmod' 'systemd-libs' 'util-linux-libs')
makedepends=('asciidoctor' 'bash-completion' 'systemd' 'xmlto')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pmem/ndctl/archive/v$pkgver.tar.gz")
sha512sums=('8fa4448cc66a156280051402ecea86a1da0f17235763de2475b5a3da622f35f469c3aed6c3f54b7f3b1553b1c86fd11e1f55e83c8c2aa0ea6d01a3ca9e503a2d')

prepare() {
  # fix location of modprobe.d conf
  sed -i 's|^modprobedir =.*|modprobedir = /usr/lib/modprobe.d/|' $pkgname-$pkgver/Makefile.am
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
