# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=klickety
pkgver=20.12.0
pkgrel=2
pkgdesc="An adaptation of the Clickomania game"
url="https://kde.org/applications/games/klickety/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kde-games)
depends=(libkdegames hicolor-icon-theme perl)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         kdebug-430362.patch::"https://invent.kde.org/games/klickety/-/commit/3d178427.patch")
sha256sums=('310baa29e713a53478c8d19c9cab97303f17320e7c01c177e5109563f23ac287'
            'SKIP'
            'd0552f4605d01a58b36796f2dc2ee7bf3cef63b335e9a1cd56532ec27d867eca')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../kdebug-430362.patch # Fix starting new game
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
