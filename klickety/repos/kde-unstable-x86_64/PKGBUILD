# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=klickety
pkgver=20.07.80
pkgrel=1
pkgdesc="An adaptation of the Clickomania game"
url="https://kde.org/applications/games/klickety/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kde-games)
depends=(libkdegames hicolor-icon-theme perl)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('d889e0542952dfa934c3148fe63811aeb2cbae2e942d6c1df38496d77c12deea'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
