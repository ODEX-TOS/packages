# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Yaron de Leeuw <me@jarondl.net>

pkgname=python-isort
pkgver=5.6.4
pkgrel=3
pkgdesc="A Python utility / library to sort Python imports."
arch=('any')
url="https://github.com/timothycrosley/isort"
license=('MIT')
depends=('python-toml')
makedepends=('python-dephell')
checkdepends=('git' 'pylama' 'python-black' 'python-hypothesis-auto' 'python-hypothesmith'
              'python-libcst' 'python-pip' 'python-pip-api' 'python-pipreqs' 'python-pytest'
              'python-pytest-mock' 'python-requirementslib')
source=("https://github.com/timothycrosley/isort/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        $pkgname-ignore-tmuxp.patch::https://github.com/PyCQA/isort/commit/7c2cb61e7ce2200a99f6852532e1f40f502d4e2c.patch)
sha512sums=('11a8eade92c5ef59ca2f15ed0ca5b965d088bb3951e14ae6470c016df1e0cf8d74e15689e9deae8b1fd131ddee62c7ddc4e86dfe7d491ac2c7f8237151abdc13'
            'e578da287361a4bd971c959888effd2d50c1128633520305f9c5607162fbe9fca184cd6fb345ef8f04cc1cef0b68d60cde8b1350266f4d8f731c4f4812196fe9')

prepare() {
  cd isort-$pkgver
  patch -p1 -i ../$pkgname-ignore-tmuxp.patch

  # Devendor toml
  rm -r isort/_vendored
  sed -i 's/from ._vendored //' isort/settings.py

  # poetry-generated setup.py are fatally broken, see:
  # https://github.com/sdispater/poetry/issues/866
  dephell deps convert --from pyproject.toml --to setup.py
  for _plugin in example_isort_formatting_plugin example_shared_isort_profile; do
    pushd $_plugin
    dephell deps convert --from pyproject.toml --to setup.py
    popd
  done
}

build() {
  cd isort-$pkgver
  python setup.py build
}

check() {
  cd isort-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  for _plugin in example_isort_formatting_plugin example_shared_isort_profile; do
    pushd $_plugin
    python setup.py install --root="$srcdir/isort-$pkgver/tmp_install" --optimize=1
    popd
  done
  # tests/test_importable.py::test_importable: This is just weird
  # tests/integration/test_projects_using_isort.py::test_airflow: https://github.com/PyCQA/isort/pull/1567#issuecomment-710787547
  PYTHONPATH="$PWD/example_isort_formatting_plugin:$PWD/example_shared_isort_profile:$PWD/tmp_install/usr/lib/python3.9/site-packages" PATH="$PWD/tmp_install/usr/bin:$PATH" \
    pytest --deselect tests/test_importable.py::test_importable --deselect tests/integration/test_projects_using_isort.py::test_airflow
}

package() {
  cd isort-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
