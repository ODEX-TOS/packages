# Maintainer: Daniel Isenmann <daniel@archlinux.org>

pkgname=babl
pkgver=0.1.78
pkgrel=1
pkgdesc="Dynamic, any to any, pixel format conversion library"
arch=(x86_64)
url="https://gegl.org/babl/"
license=(LGPL3)
depends=(glibc lcms2)
makedepends=(git meson gobject-introspection vala)
_commit=24c3666768d5530541e1dbdeb0354a37132213ff  # tags/BABL_0_1_78^0
source=("git+https://gitlab.gnome.org/GNOME/babl.git#commit=$_commit")
sha256sums=('SKIP')

# Don't port to meson until babl's runtime cpu detection works there

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^BABL_//;s/_/./g;s/-/+/g'
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
