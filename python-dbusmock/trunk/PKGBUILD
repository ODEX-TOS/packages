# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=python-dbusmock
pkgver=0.19
pkgrel=2
pkgdesc="Mock D-Bus objects for tests"
url="https://github.com/martinpitt/python-dbusmock"
arch=(any)
license=(LGPL3)
depends=(python-dbus python-gobject)
makedepends=(python-setuptools git)
_commit=38982927a6db9008cd5349fd0f03b90972abbaba  # tags/0.19^0
source=("git+https://github.com/martinpitt/python-dbusmock#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  cd $pkgname
  python setup.py build
}

package() {
  cd $pkgname
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
