# Maintainer: Firmicus <firmicus āt gmx dōt net>
# Maintainer: Rémy Oudompheng <remy@archlinux.org>

pkgname=texlive-music
pkgver=2020.54758
_revnr=${pkgver#2020.}
pkgrel=2
pkgdesc="TeX Live - Music typesetting packages"
license=('GPL')
arch=(any)
depends=('texlive-core')
optdepends=('python: for scripts from the lilyglyphs packages'
            'fontforge: for scripts from the lilyglyphs packages')
groups=('texlive-most')
url='http://tug.org/texlive/'
source=("https://sources.archlinux.org/other/texlive/$pkgname-$pkgver-src.zip" "$pkgname.maps" python3-compat.patch)
options=('!emptydirs')
sha256sums=('47f418d851a0f838c08be406135ef338533e32cf0bc9c4ffefeaa0139973e8f1'
            '0b3cc045390e756eb9243c0acc99a48a9c02ddab4764f4a00f8c3170660f017f'
            'f439a94c93eca9fa7e1e52b9d1f6af2abb963b33978bdea4b6f893cd97562392')

build() {
   for p in *.tar.xz; do
	   bsdtar -xf $p
   done
   rm -rf {tlpkg,doc,source} || true

   # Update print statements to Python 3
   cd texmf-dist
   patch -Np1 -i ${srcdir}/python3-compat.patch
}

package() {
   install -m755 -d "$pkgdir"/var/lib/texmf/arch/installedpkgs
   sed -i '/^#/d' CONTENTS
   install -m644 CONTENTS "$pkgdir"/var/lib/texmf/arch/installedpkgs/${pkgname}_${_revnr}.pkgs
   install -m644 $pkgname.maps "$pkgdir"/var/lib/texmf/arch/installedpkgs/
   install -m755 -d "$pkgdir"/usr/share
   wanteddirs=$(for d in *; do test -d $d && [[ $d != texmf* ]] && echo $d; done) || true
   for dir in $wanteddirs; do
     find $dir -type d -exec install -d -m755 "$pkgdir"/usr/share/texmf-dist/'{}' \;
     find $dir -type f -exec install -m644 '{}' "$pkgdir"/usr/share/texmf-dist/'{}' \;
   done
   if [[ -d texmf-dist ]]; then
     find texmf-dist -type d -exec install -d -m755 "$pkgdir"/usr/share/'{}' \;
     find texmf-dist -type f -exec install -m644 '{}' "$pkgdir"/usr/share/'{}' \;
   fi
   if [[ -d "$pkgdir"/usr/share/texmf-dist/scripts ]]; then
     find "$pkgdir"/usr/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \;
   fi
   # script symlinks
   install -m755 -d "$pkgdir"/usr/bin
   ln -s /usr/share/texmf-dist/scripts/lilyglyphs/lily-glyph-commands.py "$pkgdir"/usr/bin/lily-glyph-commands
   ln -s /usr/share/texmf-dist/scripts/lilyglyphs/lily-image-commands.py "$pkgdir"/usr/bin/lily-image-commands
   ln -s /usr/share/texmf-dist/scripts/lilyglyphs/lily-rebuild-pdfs.py "$pkgdir"/usr/bin/lily-rebuild-pdfs
   ln -s /usr/share/texmf-dist/scripts/m-tx/m-tx.lua "$pkgdir"/usr/bin/m-tx
   ln -s /usr/share/texmf-dist/scripts/musixtex/musixflx.lua "$pkgdir"/usr/bin/musixflx
   ln -s /usr/share/texmf-dist/scripts/musixtex/musixtex.lua "$pkgdir"/usr/bin/musixtex
   ln -s /usr/share/texmf-dist/scripts/pmxchords/pmxchords.lua "$pkgdir"/usr/bin/pmxchords
}
