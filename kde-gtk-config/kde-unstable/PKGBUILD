# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kde-gtk-config
pkgver=5.19.90
pkgrel=1
pkgdesc='GTK2 and GTK3 Configurator for KDE'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(qt5-svg kdecoration kconfigwidgets kdbusaddons)
makedepends=(extra-cmake-modules gtk2 gtk3 sassc)
optdepends=('gtk2: GTK2 apps support' 'gtk3: GTK3 apps support' 'xsettingsd: apply settings to GTK applications on the fly')
groups=(plasma)
source=("https://download.kde.org/unstable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
         kde-gtk-config-no-kservice.patch::"https://invent.kde.org/plasma/kde-gtk-config/-/commit/c6708745.patch")
sha256sums=('a12141f2323e3d676af41cbe42a45500911eb3b57cfcedae65a00112b8c34346'
            'SKIP'
            '74e6f2a958836e01c3ca949e1bb158c1525b7dd2879eab9c025741d1839b3ba6')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../kde-gtk-config-no-kservice.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
