# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kdepim-apps-libs
pkgver=20.04.2
pkgrel=1
pkgdesc="KDE PIM mail related libraries"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(libkleo pimcommon grantleetheme)
makedepends=(extra-cmake-modules boost)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('76a39e6cf75125664c413e5b749fb7b0bae5905b774f0e5a38835bd961e0caa8'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
