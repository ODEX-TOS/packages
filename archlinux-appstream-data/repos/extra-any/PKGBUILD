# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20200612
pkgrel=1
pkgdesc="Arch Linux application database for AppStream-based software centers"
arch=(any)
url="https://www.archlinux.org"
license=(GPL)
depends=()
makedepends=()
source=()
noextract=()
for _repo in core extra community; do
 source+=($_repo-$pkgver.xml.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/Components-x86_64.xml.gz
          $_repo-icons-48x48-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-48x48.tar.gz
          $_repo-icons-64x64-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-64x64.tar.gz
          $_repo-icons-128x128-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-128x128.tar.gz)
 noextract+=($_repo.xml.gz-$pkgver $_repo-icons-{48x48,64x64,128x128}-$pkgver.tar.gz)
done
sha256sums=('e81c10b606c65dff81e88a364643d92b0bd59d05cc4feefac6c3bf295e5a7946'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '03025ef4261aeea213b938161aad2e8fa5aca53682e6a6f2997fef5a715bf662'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '82aa02ffee757ce9ac839718080520978f24c50216d22303198287caa4310fbf'
            '14505b1b2a40bbc0ec1934a0952bb9112bc78572a2a9c48f8fae28b6e833198d'
            '124c55869980046e4d8a9698e3c900e5272102aad99258f7a0aa04ea07a7ff7c'
            '4e89ba5b46d2faad6488cd78dfd25d05c4254ddc09545cb8c7e995b3d79bbc20'
            '58594d82e0375581ab31dfb5b94fd83403a46ff8645647342e67d44a53b44410'
            'e4dd1eb3ad6283ec078cc3a9eec8bee8b6c1234bb61bcb814aae280ce9010c0b'
            '074ec173b474bb17e1bb32dc4dbd02a5442a23bf32f9c77cb61a77c514ca31c4'
            'a409ffef110c1bda8c08262c449689cff747e2d001610f83584becc813f7a028')

package() {
  mkdir -p "$pkgdir"/usr/share/app-info/{icons/archlinux-arch-{core,extra,community}/{48x48,64x64,128x128},xmls}
  for _repo in core extra community; do
   tar -xzf $_repo-icons-48x48-$pkgver.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/48x48
   tar -xzf $_repo-icons-64x64-$pkgver.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128-$pkgver.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/128x128
   install -m644 $_repo-$pkgver.xml.gz "$pkgdir"/usr/share/app-info/xmls/$_repo.xml.gz
  done
}
