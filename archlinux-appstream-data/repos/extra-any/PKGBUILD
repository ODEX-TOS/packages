# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20201128
pkgrel=1
pkgdesc="Arch Linux application database for AppStream-based software centers"
arch=(any)
url="https://www.archlinux.org"
license=(GPL)
depends=()
makedepends=()
source=()
noextract=()
for _repo in core extra community; do
 source+=($_repo-$pkgver.xml.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/Components-x86_64.xml.gz
          $_repo-icons-48x48-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-48x48.tar.gz
          $_repo-icons-64x64-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-64x64.tar.gz
          $_repo-icons-128x128-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-128x128.tar.gz)
 noextract+=($_repo.xml.gz-$pkgver $_repo-icons-{48x48,64x64,128x128}-$pkgver.tar.gz)
done
sha256sums=('cad128d0211b7c0a070b9dcde63c86329fb2dae81fd40ceb5ecefd05cac56c90'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '283a80897bd70ee4e8757c393693635fc36b1973d325940f4f3079561cceed92'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '7287ce4bb231d771baa00a8c189310c01ef2bfadf3791963a044a20ef2f20a51'
            '60d6ec4a41d6d5d1d94e260bff5cda3ae5d1d88de510ac1ee49cb5aaa4fb9822'
            '1a00a8ff9059b7e5e10e2e751dbef4157247ec0490734daf354ae6e303e45f62'
            'c10866bafa0d7fe6cfb466f046d419044f35d7697cf836bce3e09cfde1250c5e'
            'b6cc45b43dca30b54a828bf222fef30d442197b52644a70db0215a40d7068b99'
            '8d3c062de300c8a4feb1044be273c00b8a4f48d5f37011a51aa6e29f116fa2e1'
            '787b7c1424fbfbfb26be76a324c15f66466e41abd98756c832bf37353e56b906'
            'ed62defcb3a6e966732c963c13c606e90440cb37c5920957dc1e07c65960c1fc')

package() {
  mkdir -p "$pkgdir"/usr/share/app-info/{icons/archlinux-arch-{core,extra,community}/{48x48,64x64,128x128},xmls}
  for _repo in core extra community; do
   tar -xzf $_repo-icons-48x48-$pkgver.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/48x48
   tar -xzf $_repo-icons-64x64-$pkgver.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128-$pkgver.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/128x128
   install -m644 $_repo-$pkgver.xml.gz "$pkgdir"/usr/share/app-info/xmls/$_repo.xml.gz
  done
}
