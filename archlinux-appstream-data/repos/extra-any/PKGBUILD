# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20200720
pkgrel=1
pkgdesc="Arch Linux application database for AppStream-based software centers"
arch=(any)
url="https://www.archlinux.org"
license=(GPL)
depends=()
makedepends=()
source=()
noextract=()
for _repo in core extra community; do
 source+=($_repo-$pkgver.xml.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/Components-x86_64.xml.gz
          $_repo-icons-48x48-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-48x48.tar.gz
          $_repo-icons-64x64-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-64x64.tar.gz
          $_repo-icons-128x128-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-128x128.tar.gz)
 noextract+=($_repo.xml.gz-$pkgver $_repo-icons-{48x48,64x64,128x128}-$pkgver.tar.gz)
done
sha256sums=('7f510c5b74c4696bebbc0759adc465ed44ea22c2a5d472d6ef2b0e7d27b90440'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '6d752386d4bb3b09c37ec79f0a48435a0174de94ea804952444fabddbf74c90c'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '2bf0c656e3bc4787a44b71788acb9d5431b5a9d1aa254c41d7995a91d73e0cad'
            '464e41f24d6814bf3b329a45cb3a19362faf9edc2ad4ded1bcd72df18234362e'
            'f9a8c2d5525051e0011d5f3b5dcdc3898ae2f821311e73a584e52ed56b4edec6'
            'b1ef771045bc43b34f6a96779e89340c787fb71c1f9aec18a91477d3bf00840b'
            '6ea0f42779012b3219f6a070328892b9af75268ded936179a18d7a94309e5645'
            '6a32bb50d5f462d285c41bf9e9a53f5fddf041f213ed39d75c98515da17bf6bc'
            '8f95bdf7aad27d0b67b3eb9d278ff798b7c076ea8be5833ba18eaec609b3a3fb'
            'ba85c1be0fe0da97139d36a24746c408a5f59b5726ccad2e3421dc5081c516d1')

package() {
  mkdir -p "$pkgdir"/usr/share/app-info/{icons/archlinux-arch-{core,extra,community}/{48x48,64x64,128x128},xmls}
  for _repo in core extra community; do
   tar -xzf $_repo-icons-48x48-$pkgver.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/48x48
   tar -xzf $_repo-icons-64x64-$pkgver.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128-$pkgver.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/128x128
   install -m644 $_repo-$pkgver.xml.gz "$pkgdir"/usr/share/app-info/xmls/$_repo.xml.gz
  done
}
