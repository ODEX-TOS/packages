# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgbase=libsigc++
pkgname=(libsigc++ libsigc++-docs)
pkgver=2.10.4
pkgrel=2
pkgdesc="Callback Framework for C++"
url="https://libsigcplusplus.github.io/libsigcplusplus/"
arch=(x86_64)
license=(LGPL)
depends=(gcc-libs)
makedepends=(git mm-common meson)
options=(!emptydirs)
_commit=230257ff751e018463919647c01e8e4a02e2eb15  # tags/2.10.4^0
source=("git+https://github.com/libsigcplusplus/libsigcplusplus#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd libsigcplusplus
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd libsigcplusplus
}

build() {
  arch-meson libsigcplusplus build -D maintainer-mode=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

# Do not remove the space before the () or commitpkg will
# accidentally to run this function on the system (!!!) 
package_libsigc++ () {
  provides=("libsigc++2.0=${pkgver}")
  replaces=('libsigc++2.0')
  conflicts=('libsigc++2.0')

  DESTDIR="$pkgdir" meson install -C build

  mkdir -p docs/usr/share
  mv "$pkgdir"/usr/share/{devhelp,doc} docs/usr/share
}

package_libsigc++-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  provides=("libsigc++2.0-docs=${pkgver}")
  replaces=('libsigc++2.0-docs')
  conflicts=('libsigc++2.0-docs')
  options=(!strip)

  mv docs/* "$pkgdir"
}
