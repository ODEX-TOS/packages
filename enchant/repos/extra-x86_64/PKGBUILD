# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=enchant
pkgver=2.2.12
pkgrel=1
pkgdesc="A wrapper library for generic spell checking"
arch=('x86_64')
url="https://abiword.github.io/enchant/"
license=('LGPL')
depends=('glib2')
makedepends=('boost' 'aspell' 'hunspell' 'hspell' 'nuspell' 'libvoikko')
checkdepends=('unittestpp')
optdepends=('aspell: for aspell based spell checking support'
           'hunspell: for hunspell based spell checking support'
           'libvoikko: for libvoikko based spell checking support'
           'hspell: for hspell based spell checking support'
           'nuspell: for nuspell based spell checking support')
provides=('libenchant-2.so') # enchant_voikko.so enchant_nuspell.so enchant_hunspell.so enchant_hspell.so enchant_aspell.so)
source=("https://github.com/AbiWord/enchant/releases/download/v$pkgver/enchant-$pkgver.tar.gz")
sha512sums=('ac284ed9649b11a13ff529db68cef8da829b5ff1c46ccfff36e5770fcd762055c1a75f6afbf1d2cdb00c3d8ae1cf7f336d1ff794de8f430dc2e89bb805525211')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -vfi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --disable-static \
    --enable-relocatable
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
