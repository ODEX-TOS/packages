# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=perl-libwww
pkgver=6.50
pkgrel=1
pkgdesc="The World-Wide Web library for Perl"
arch=('any')
url="https://metacpan.org/release/libwww-perl"
license=('PerlArtistic' 'GPL')
depends=('perl' 'perl-encode-locale' 'perl-file-listing'
         'perl-html-parser' 'perl-http-cookies' 'perl-http-daemon'
         'perl-http-date' 'perl-http-negotiate' 'perl-lwp-mediatypes'
         'perl-net-http' 'perl-try-tiny' 'perl-uri' 'perl-www-robotrules')
optdepends=('perl-lwp-protocol-https: for https:// url schemes')
checkdepends=('perl-test-fatal' 'perl-test-needs' 'perl-test-requiresinternet')
options=('!emptydirs')
source=(https://cpan.metacpan.org/authors/id/O/OA/OALDERS/libwww-perl-$pkgver.tar.gz)
sha512sums=('c8c4cdea71a8be67f58d1214f3d1892a23b52509f89cce745538108f5a6c754c4598091077b915f040b9e85ac35ce9926e8798bfdf6e10cf7d5f495e482143ff')

build() {
  cd libwww-perl-$pkgver
  perl Makefile.PL --aliases INSTALLDIRS=vendor
  make
}

check()  {
  cd libwww-perl-$pkgver
  make test
}

package() {
  cd libwww-perl-$pkgver
  make DESTDIR="$pkgdir" install
}
