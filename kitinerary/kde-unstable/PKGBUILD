# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kitinerary
pkgver=20.07.80
pkgrel=1
pkgdesc="Data model and extraction system for travel reservation information"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(kmime kpkpass kcalendarcore kcontacts poppler libphonenumber zxing-cpp)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('ab0c04c4e671fddff5f49c5f9a280a30551d163cb77244e4149d4d77db587836'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
