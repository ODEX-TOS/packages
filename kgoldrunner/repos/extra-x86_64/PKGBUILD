# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kgoldrunner
pkgver=20.04.3
pkgrel=1
pkgdesc="A game of action and puzzle solving"
url="https://kde.org/applications/games/kgoldrunner/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kde-games)
depends=(libkdegames hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('ab535067a8991a94bd7cd5a990a6120538206d241c8e8149c7b774cbdd1d0941'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
