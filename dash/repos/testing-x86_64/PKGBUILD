# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Dan McGee <dan@archlinux.org>

pkgname=dash
pkgver=0.5.11
pkgrel=1
pkgdesc='POSIX compliant shell that aims to be as small as possible'
url='http://gondor.apana.org.au/~herbert/dash/'
arch=('x86_64')
license=('BSD')
depends=('glibc')
install=dash.install
source=(http://gondor.apana.org.au/~herbert/dash/files/${pkgname}-${pkgver}.tar.gz)
sha512sums=('41c3afa8aa4788f7899b9a4c1bce2dc8629f4405184cdc7269993e72c695cf9537c9e68b654d56f06c604a82ffa73c9160b91368e82a3593a12ff7dea10505d1')
b2sums=('0d399b35c10a33f6a22f51f7b74b9e092fb11d47ae45948f43fab0dd1416061145e7f930f0f0d1119d02cc45790ba19ce040c951322c131e706e5f3f32f2612b')

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --bindir=/usr/bin \
    --mandir=/usr/share/man \
    --exec-prefix="" \
    --with-libedit
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
