# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gjs
pkgver=1.64.4
pkgrel=1
epoch=2
pkgdesc="Javascript Bindings for GNOME"
url="https://wiki.gnome.org/Projects/Gjs"
arch=(x86_64)
license=(GPL)
depends=(cairo gobject-introspection-runtime js68 dconf readline)
makedepends=(gobject-introspection git sysprof meson)
checkdepends=(valgrind xorg-server-xvfb)
provides=(libgjs.so)
_commit=b35f834a27d31dec27cb71662da60637854020c4  # tags/1.64.4^0
source=("git+https://gitlab.gnome.org/GNOME/gjs.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build --buildtype release -D installed_tests=false
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run \
    -s '-screen 0 1920x1080x24 -nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  depends+=(libreadline.so)
  DESTDIR="$pkgdir" meson install -C build
}
