# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kdav
pkgver=5.73.0
pkgrel=1
epoch=1
pkgdesc="A DAV protocol implemention with KJobs"
arch=(x86_64)
url="https://community.kde.org/Frameworks"
license=(LGPL)
depends=(kio qt5-xmlpatterns)
makedepends=(extra-cmake-modules doxygen qt5-tools)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('03c8af96e7bb1b1d2d633e54c6362c7c2de078b8aba5654042b7a11d968efa31'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
