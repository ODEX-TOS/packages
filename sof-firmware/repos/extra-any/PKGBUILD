# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=sof-firmware
pkgver=1.5.1
pkgrel=1
pkgdesc="Sound Open Firmware"
arch=(any)
url="https://www.sofproject.org/"
license=(custom:BSD custom:ISC)
options=(!strip)
_tarname=sof-bin-stable-v$pkgver
source=($_tarname.tar.gz::https://github.com/thesofproject/sof-bin/archive/stable-v$pkgver.tar.gz)
sha256sums=('629d33749b9d94c2fdf81daa9888123df53a9c3a40b77daf68ff5a03532f74e8')

prepare() {
  cd $_tarname
  sed -e '/^ROOT=/d' -e "/^VERSION=/s/=.*/=v$pkgver/" -i go.sh
}

package() {
  cd $_tarname
  mkdir -p "$pkgdir/usr/lib/firmware"
  ROOT="$pkgdir/usr" bash go.sh
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENCE* Notice*
}

# vim:set sw=2 et:
