# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=x11vnc
epoch=1
pkgver=0.9.16
pkgrel=3
pkgdesc='VNC server for real X displays'
url='https://github.com/LibVNC/x11vnc'
arch=('x86_64')
license=('GPL2')
optdepends=('tk: GUI support'
            'net-tools: -auth guess'
            'xf86-video-dummy: Xdummy script')
depends=('libvncserver' 'openssl' 'libjpeg' 'libxtst' 'libxinerama'
         'libxdamage' 'libxrandr' 'avahi' 'xorg-xdpyinfo')
source=("https://github.com/LibVNC/x11vnc/archive/${pkgver}.tar.gz"
        "$pkgname-xfc-null-ptr.patch::https://github.com/LibVNC/x11vnc/commit/95a10ab64c2d.patch"
        "$pkgname-gcc10.patch::https://github.com/LibVNC/x11vnc/commit/a48b0b1cd887.patch"
        'service')
sha256sums=('885e5b5f5f25eec6f9e4a1e8be3d0ac71a686331ee1cfb442dba391111bd32bd'
            'eaa4678185dee4e2df6db8c74b5f5159ca982050825093362a7be79b4552ebf8'
            '9358217c23e586cb34d98564e5031dd58bf43e621d23435629d2105c901d0aa8'
            'cfb19d44e09e960e2fdb958c9258bccf23c2677715314985f7e819f1dcedb6e4')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i ../$pkgname-xfc-null-ptr.patch # FS#65851
	patch -p1 -i ../$pkgname-gcc10.patch
	autoreconf --force --install
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --mandir=/usr/share/man
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install misc/{rx11vnc,Xdummy} "${pkgdir}"/usr/bin
	install -Dm644 ../service "${pkgdir}/usr/lib/systemd/system/x11vnc.service"
}
