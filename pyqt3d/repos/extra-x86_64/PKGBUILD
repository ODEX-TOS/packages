# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=pyqt3d
pkgname=(python-pyqt3d)
pkgver=5.15.1
pkgrel=1
pkgdesc="Python bindings for Qt3D"
arch=(x86_64)
url="https://www.riverbankcomputing.com/software/pyqt3d/intro"
license=(GPL3)
groups=(pyqt5)
depends=(qt5-3d python-pyqt5)
makedepends=(sip5 pyqt-builder)
source=("https://pypi.python.org/packages/source/P/PyQt3D/PyQt3D-$pkgver.tar.gz")
sha256sums=('f6dddf29b910a3638074a0fa783d63613c84899b8e07498c5afdfcb815f96eed')

build() {
  cd "$srcdir"/PyQt3D-$pkgver
  sip-build \
    --no-make \
    --api-dir /usr/share/qt/qsci/api/python
  cd build
  make
}

package_python-pyqt3d(){
  cd PyQt3D-$pkgver/build
  make INSTALL_ROOT="$pkgdir" install -j1
}
