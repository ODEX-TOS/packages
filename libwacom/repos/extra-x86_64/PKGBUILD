# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libwacom
pkgver=1.4
pkgrel=2
pkgdesc="Library to identify Wacom tablets and their features"
arch=('x86_64')
url="https://github.com/linuxwacom/libwacom/wiki"
license=('MIT')
depends=('glib2' 'systemd' 'libgudev')
makedepends=('libxml2')
validpgpkeys=('3C2C43D9447D5938EF4551EBE23B7E70B467F0BF')
source=(https://github.com/linuxwacom/libwacom/releases/download/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.bz2{,.sig}
        libwacom-fix-gamepad.patch::"https://patch-diff.githubusercontent.com/raw/linuxwacom/libwacom/pull/272.patch")
sha512sums=('db8daade5843a236360ea1f1ba7611f7e66e663173e1a07422b5b309060ef0ccd3367d5913a171d266006ff68bf154dbd06e9a80cfa013c859b10b8394454691'
            'SKIP'
            'c7e343fe2b1d021b4ca0f06f409dab06594766ebcc14ddc1bc7a9ebffc19b1912e84c736262b99ed2208b9dacf8a8bf9f0f9d981c6718124016bf9d460e774d7')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../libwacom-fix-gamepad.patch # Fix gamepad regression
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -m755 -d "${pkgdir}"/usr/lib/udev/rules.d
  cd tools
  ./generate-hwdb > "${pkgdir}"/usr/lib/udev/hwdb.d/65-libwacom.hwdb
}
