# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=tali
pkgver=3.38.0
pkgrel=1
pkgdesc="Beat the odds in a poker-style dice game"
url="https://wiki.gnome.org/Apps/Tali"
arch=(x86_64)
license=(GPL)
depends=(gtk3 librsvg libgnome-games-support)
makedepends=(meson gobject-introspection yelp-tools appstream-glib git)
groups=(gnome-extra)
_commit=6a12d416beb7e8f68b9cdeb66153fd52551125a0  # tags/3.38.0^0
source=("git+https://gitlab.gnome.org/GNOME/tali.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
