# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=scour
pkgver=0.38
pkgrel=1
pkgdesc='An SVG scrubber'
arch=(any)
url=https://github.com/scour-project/scour
license=(Apache)
depends=(
  python-six
)
makedepends=(
  git
  python-setuptools
)
_commit=c84731e12d4af0e219c4389434ed68f6708d8c33
source=(git+https://github.com/scour-project/scour.git#commit=${_commit})
sha256sums=(SKIP)

pkgver() {
  cd scour

  git describe --tags | sed 's/^v//'
}

build() {
  cd scour
  python setup.py build
}

package() {
  cd scour
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
