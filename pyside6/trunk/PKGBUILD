# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=pyside6
_qtver=6.0.0
pkgver=${_qtver/-/}
pkgrel=2
arch=(x86_64)
url='https://www.qt.io'
license=(LGPL)
pkgdesc='Enables the use of Qt6 APIs in Python applications'
depends=(shiboken6 qt6-declarative)
makedepends=(cmake python qt6-tools qt6-quickcontrols2 qt6-svg qt6-3d)
optdepends=('qt6-tools: QtHelp, QtUiTools bindings' 'qt6-quickcontrols2: QtQuickControls2 bindings' 'qt6-svg: QtSvg bindings' 'qt6-3d: Qt3D bindings')
_pkgfn=pyside-setup-opensource-src-$_qtver
source=("https://download.qt.io/official_releases/QtForPython/pyside6/PySide6-$pkgver-src/${_pkgfn}.tar.xz"
         pyside6-qt3d.patch::"https://code.qt.io/cgit/pyside/pyside-setup.git/patch/sources?id=625417c4")
sha256sums=('f47a4598118510feecc96b9d4b01063d206ac9a1240af3545163d2891db27dd4'
            '268ffc922237edbd89623ffacb698f642086f3da92447b5cb685c5c5e7f331c4')

prepare() {
  patch -d $_pkgfn -p1 -i ../pyside6-qt3d.patch # Fix build of Qt3D bindings
}

build() {
  cmake -B build -S $_pkgfn/sources/pyside6 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=OFF \
    -DPYTHON_EXECUTABLE=/usr/bin/python
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
