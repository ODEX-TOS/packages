# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=pyside6
_qtver=6.0.0
pkgver=${_qtver/-/}
pkgrel=1
arch=(x86_64)
url='https://www.qt.io'
license=(LGPL)
pkgdesc='Enables the use of Qt6 APIs in Python applications'
depends=(shiboken6 qt6-declarative)
makedepends=(cmake python qt6-tools qt6-quickcontrols2 qt6-svg)
optdepends=('qt6-tools: QtHelp, QtUiTools bindings' 'qt6-quickcontrols2: QtQuickControls2 bindings' 'qt6-svg: QtSvg bindings')
_pkgfqn=pyside-setup-opensource-src-$_qtver
source=("https://download.qt.io/official_releases/QtForPython/pyside6/PySide6-$pkgver-src/${_pkgfqn}.tar.xz")
sha256sums=('f47a4598118510feecc96b9d4b01063d206ac9a1240af3545163d2891db27dd4')

build() {
  cmake -B build -S ${_pkgfqn}/sources/pyside6 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=OFF \
    -DPYTHON_EXECUTABLE=/usr/bin/python
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
