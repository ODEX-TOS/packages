# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=automake
pkgver=1.16.2
pkgrel=3
pkgdesc="A GNU tool for automatically creating Makefiles"
arch=('any')
license=('GPL')
url="https://www.gnu.org/software/automake"
groups=('base-devel')
depends=('perl' 'bash')
makedepends=('autoconf')
checkdepends=('dejagnu' 'gcc-fortran' 'java-environment' 'vala' 'emacs' 'cscope'
              'expect' 'ncompress' 'gettext' 'lzip' 'zip' 'sharutils' 'help2man'
              # disable TeX tests for now, lots of them fail and need upstream
              # fixes for current texlive release
              #'texlive-bin' 'texinfo'
              'python' 'python-virtualenv')
source=("https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz"{,.sig}
        test_vala-mix2.sh.diff)
sha512sums=('a4aa0e41ceaa7df5bc303a6004597fb158f4198594017cd2c586fd9f5a29233e081766bf22b7e4ef0d4c8c3d45a8591009427efa319b362922a958ac1ef6e27b'
            'SKIP'
            'e6cc9cb41cfd10cbd4ab0263859bfa8c892112c68a69043601c6d7da7c70300ccfe29b242a597bdf6c00bf14c12c19180eedbd66e80ae2bbea9513e36d999108')
validpgpkeys=('E1622F96D2BB4E58018EEF9860F906016E407573'   # Stefano Lattarini
              'F2A38D7EEB2B66405761070D0ADEE10094604D37'   # Mathieu Lirzin
              '155D3FC500C834486D1EEA677FD9FCCB000BEEEE')  # Jim Meyering

prepare() {
	# upstream suggested gcc10 fix for failing test
	# https://lists.gnu.org/archive/html/bug-automake/2020-06/msg00003.html
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -Np1 -i ../test_vala-mix2.sh.diff
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --build=$CHOST --prefix=/usr
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
