# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kpimtextedit
pkgver=20.08.0
pkgrel=1
pkgdesc="A textedit with PIM-specific features"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(kio syntax-highlighting)
makedepends=(extra-cmake-modules qt5-tools)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('3ae3b9f8a980d10e366351efaf2b186fd911402da5b5ee634f4687d43586ce34'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
