# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=baobab
pkgver=3.34.1
pkgrel=1
pkgdesc="A graphical directory tree analyzer"
url="https://wiki.gnome.org/Apps/DiskUsageAnalyzer"
arch=(x86_64)
license=(GPL2)
depends=(dconf gtk3 gsettings-desktop-schemas librsvg)
makedepends=(vala gobject-introspection yelp-tools appstream-glib git meson)
groups=(gnome)
_commit=be3b5b6e32ce1d61929c7f26990a40f818effee1  # tags/3.34.1^0
source=("git+https://gitlab.gnome.org/GNOME/baobab.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build

  # https://bugs.archlinux.org/task/31861
  sed -i '/^MimeType=/d' "$pkgdir/usr/share/applications/org.gnome.baobab.desktop"
}
