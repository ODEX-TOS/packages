# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kmailtransport
pkgver=20.07.80
pkgrel=1
pkgdesc="Mail Transport Service"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(akonadi-mime ksmtp libkgapi)
makedepends=(extra-cmake-modules boost)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('54870f627705beb9ef13e07f15a099668c0add9082d076bab049f79b3da9ec2e'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
