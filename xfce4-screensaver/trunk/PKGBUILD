# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=xfce4-screensaver
pkgver=0.1.11
pkgrel=2
pkgdesc='Xfce Screensaver'
arch=('x86_64')
url='https://docs.xfce.org/apps/screensaver/start'
license=('GPL2')
groups=('xfce4-goodies')
depends=('libxss' 'libxklavier' 'libwnck3' 'garcon' 'dbus-glib' 'python' 'python-gobject')
makedepends=('intltool')
backup=('etc/pam.d/xfce4-screensaver')
source=("https://archive.xfce.org/src/apps/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2"
        '0001-Catch-gs_listener_dbus_init-failures.patch')
sha256sums=('814020da160ac4f9bcd79ae098ca6dd17643144214613dcb687821c8d8deac77'
            '8037d7f257e30a85b0acff2f0697973ed46e17c567f3fdde87b52437eaad6208')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 -R < ../0001-Catch-gs_listener_dbus_init-failures.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}
