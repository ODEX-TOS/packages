# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgname=vulkan-icd-loader
pkgver=1.2.153
pkgrel=2
arch=(x86_64)
pkgdesc="Vulkan Installable Client Driver (ICD) Loader"
url="https://www.khronos.org/vulkan/"
license=('custom')
makedepends=(cmake python-lxml libx11 libxrandr wayland vulkan-headers git)
depends=(glibc)
optdepends=('vulkan-driver: packaged vulkan driver') # vulkan-driver: vulkan-intel/vulkan-radeon/nvidia-utils/....
source=("https://github.com/KhronosGroup/Vulkan-Loader/archive/v${pkgver}.tar.gz"
        0001-Revert-loader-Return-error-if-layer-loading-failed.patch)
#source=("git+https://github.com/KhronosGroup/Vulkan-Loader.git#commit=8df99b31f01ef1ef7fb17082dd577e77f20cb050")
sha256sums=('5fb906b2dc968f2256f2d09b093ec8cc7f19812d656c649de8ed709a6da63d4a'
            '0450033ffcd8970ee1d69cf97c989b48e9b675b0fb71197f5e7b39c816b9c4aa')

#pkgver() {
#  cd "${srcdir}"/Vulkan-Loader*
#
#  echo 1.1.106+$(git rev-list --count HEAD)+$(git rev-parse --short HEAD)
#}

prepare() {
  cd "${srcdir}"/Vulkan-Loader*

  #Â https://github.com/ValveSoftware/steam-for-linux/issues/7368
  patch -Np1 -i ../0001-Revert-loader-Return-error-if-layer-loading-failed.patch
}

build() {
  cd "${srcdir}"/Vulkan-Loader*

  rm -rf build ; mkdir build ; cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DVULKAN_HEADERS_INSTALL_DIR=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_DATADIR=/share \
    -DCMAKE_SKIP_RPATH=True \
    -DBUILD_TESTS=Off \
    -DBUILD_WSI_XCB_SUPPORT=On \
    -DBUILD_WSI_XLIB_SUPPORT=On \
    -DBUILD_WSI_WAYLAND_SUPPORT=On \
    -DCMAKE_BUILD_TYPE=Release \
    ..
  make
}

package() {
  cd "${srcdir}"/Vulkan-Loader*/build
  
  make DESTDIR="${pkgdir}" install
  
  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 ../LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/
}
