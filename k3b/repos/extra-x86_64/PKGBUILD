# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=k3b
pkgver=20.08.1
pkgrel=2
epoch=1
pkgdesc="Feature-rich and easy to handle CD burning application"
arch=(x86_64)
url="https://userbase.kde.org/K3b"
license=(GPL)
depends=(libkcddb kcmutils knotifyconfig libmad kfilemetadata knewstuff
         libmpcdec libdvdread libburn qt5-webkit)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('cdrtools: for CD burning with cdrecord'
            'dvd+rw-tools: for DVD burning support'
            'vcdimager: for VCD burning support'
            'emovix: for bootable multimedia CD/DVD support'
            'cdrdao: for disk-at-once (DAO) mode support'
            'cdparanoia: for CD ripping support' 'transcode: for DVD ripping support')
groups=(kde-applications kde-multimedia)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         k3b-fix-auth-install.patch::"https://invent.kde.org/multimedia/k3b/commit/beef1480.patch")
sha256sums=('66fad59f4e35c24dc8868c46ff45ffa66048501e0a6ac0c23a8b9e957033e50f'
            'SKIP'
            '3ba593f923bee41ee184283184b938c3b23471bd7de4a9fe0a0883a997e9d365')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../k3b-fix-auth-install.patch # install kauth helper to its expected location
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
