# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Chirantan Ekbote <chirantan.ekbote at gmail.com>
# Contributor: Kringel

pkgname=eigen
pkgver=3.3.7
pkgrel=7
pkgdesc="Lightweight C++ template library for vector and matrix math, a.k.a. linear algebra."
arch=('any')
url='https://eigen.tuxfamily.org'
license=('GPL3' 'LGPL2.1' 'BSD' 'custom:MPL2' 'custom:MINPACK')
makedepends=('cmake' 'pkg-config' 'freeglut' 'gcc-fortran' 'fftw' 'suitesparse' 'boost')
source=("${pkgname}-${pkgver}.tar.bz2::https://bitbucket.org/eigen/eigen/get/${pkgver}.tar.bz2"
         eigen-c++17.patch::"https://gitlab.com/libeigen/eigen/-/commit/b3c4344a.patch"
         eigen-c++20.patch::"https://gitlab.com/libeigen/eigen/-/commit/867a7565.patch")
sha256sums=('9f13cf90dedbe3e52a19f43000d71fdf72e986beb9a5436dddcd61ff9d77a3ce'
            '80c1e974e525c3327ae933d6f064c8fb270d8ca99e32369533aacff72ee9cfce'
            '2b1929b165302be43614f86286712402a4cbfa4d176b3cafba9588d1d84dd210')

prepare() {
  cd eigen-eigen-*
  patch -p1 -i ../eigen-c++17.patch # https://gitlab.com/libeigen/eigen/-/issues/1676  
  patch -p1 -i ../eigen-c++20.patch # https://gitlab.com/libeigen/eigen/-/issues/1833
}

build() {
  cmake -B build -S eigen-eigen-* \
    -DCMAKE_INSTALL_PREFIX=/usr
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # install custom licenses
  install -Dm644 eigen-eigen-*/COPYING.MPL2 -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 eigen-eigen-*/COPYING.BSD -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 eigen-eigen-*/COPYING.MINPACK -t "$pkgdir/usr/share/licenses/$pkgname"
}
