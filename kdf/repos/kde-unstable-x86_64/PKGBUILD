# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdf
pkgver=20.07.80
pkgrel=1
url="https://kde.org/applications/system/kdiskfree/"
arch=(x86_64)
pkgdesc='View Disk Usage'
license=(GPL LGPL FDL)
depends=(kio hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools kcmutils)
groups=(kde-applications kde-utilities)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('f89764582125d52f1ba986c34782d880a10728213f438e487dc60b099344ee26'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
