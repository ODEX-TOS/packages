# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: anex <assassin.anex[@]gmail.com

pkgname=signon-kwallet-extension
pkgver=20.04.2
pkgrel=1
pkgdesc="KWallet integration for signon framework"
arch=(x86_64)
url="https://kde.org"
license=(LGPL)
groups=(kde-applications kde-network)
depends=(signond kwallet)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('36187382e0fa6179b030f47ff6db3b764ddce76f3c5929180ec26f91e8ec6fec'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
