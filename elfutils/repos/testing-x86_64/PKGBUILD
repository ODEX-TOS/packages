# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>

pkgbase=elfutils
pkgname=(elfutils libelf)
pkgver=0.180
pkgrel=1
pkgdesc='Utilities to handle ELF object files and DWARF debugging information'
arch=(x86_64)
url='https://sourceware.org/elfutils/'
license=(LGPL3 GPL GPL3)
makedepends=(gcc-libs zlib bzip2 xz libmicrohttpd sqlite libarchive curl)
options=(staticlibs)
source=(https://sourceware.org/elfutils/ftp/${pkgver}/elfutils-${pkgver}.tar.bz2{,.sig})
sha512sums=('62e96035ccfe8928baca2285decbe8b8703a2daa956df81ece18fecf643272fb68955806b3e807a514141a7a9bf44520bf09461672aa580bd6807485fb604d25'
            'SKIP')
validpgpkeys=('47CC0331081B8BC6D0FD4DA08370665B57816A6A')  # Mark J. Wielaard <mark@klomp.org>
validpgpkeys+=('EC3CFE88F6CA0788774F5C1D1AA44BE649DE760A') # Mark Wielaard <mjw@gnu.org>

build() {
  cd $pkgbase-$pkgver

  CFLAGS+=" -g"  # required for test-suite success
  ./configure --prefix=/usr --program-prefix="eu-" --enable-deterministic-archives

  make
}

check() {
  cd $pkgbase-$pkgver

  # Some tests fail due to limited capabilities of build chroot
  make check || :
}

package_elfutils() {
  depends=(gcc-libs zlib bzip2 xz libmicrohttpd sqlite libarchive curl
           "libelf=$pkgver-$pkgrel")

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install

  mkdir "$srcdir/libelf" 
  mv "$pkgdir"/usr/{lib,include} "$srcdir/libelf/"
}

package_libelf() {
  pkgdesc='Libraries to handle ELF object files and DWARF debugging information'
  depends=(gcc-libs zlib bzip2 xz curl)

  mv libelf "$pkgdir/usr"
}
