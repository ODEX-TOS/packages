# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=fluidsynth
pkgver=2.1.5
pkgrel=2
pkgdesc="A real-time software synthesizer based on the SoundFont 2 specifications"
arch=('x86_64')
url="http://www.fluidsynth.org/"
license=('LGPL2.1')
groups=('pro-audio')
depends=('glibc' 'sdl2')
makedepends=('alsa-lib' 'cmake' 'dbus' 'doxygen' 'glib2' 'jack' 'ladspa'
'libinstpatch' 'libpulse' 'libsndfile' 'portaudio' 'readline' 'systemd-libs')
provides=('libfluidsynth.so' 'soundfont-synthesizer')
backup=("etc/conf.d/$pkgname")
source=("$pkgname-$pkgver.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v$pkgver.tar.gz"
        "${pkgname}-2.1.5-systemd_user_unit.patch::https://github.com/FluidSynth/fluidsynth/pull/676/commits/777f9ea5b4a9d757a3e6634e700dd96703ea5b45.patch")
sha512sums=('171ee6b1983ab9636086fb414ae464508f7a9a412cd2c1e7800a19a84d3dff21bbd86d1971f9bb5985e977f808b60e0d0cfbde4551b85fcc4aae4928a1abadbc'
            'edbfe794a1733ba622110660a410de546462a7c84c5ac3f2c0af3c66a1b8fc947b4796782463636e31981cb12e5c96ab07ecf540dc62f968221e2d6c3b9eca79')
b2sums=('f5669f49db481ded6d953ee2cdaa8c59bb751f79210d993b69967cca1b71dce12b50fbf258512c144b1578441e78cc5299d96004da5fed44a40fc33858254bc0'
        '3cfec84d25b92327881d76ba52e2b90d11aae9b41f8ae5dfe0cd83469a478a8c020cd454a90ec66c8925317e7b00e32e5b0a99edc2f65a449787ee5b8636b70c')

prepare() {
  cd "${pkgname}-${pkgver}"
  # fix systemd user unit install target:
  # https://bugs.archlinux.org/task/67955
  patch -Np1 -i "../${pkgname}-2.1.5-systemd_user_unit.patch"
}

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -DFLUID_DAEMON_ENV_FILE=/etc/conf.d/fluidsynth \
        -Denable-ladspa=ON \
        -Denable-portaudio=ON \
        -DLIB_SUFFIX="" \
        -Wno-dev \
        -B build \
        -S .
  make -C build VERBOSE=1
}

check() {
  cd "${pkgname}-${pkgver}"
  make -C build -k check
}

package() {
  depends+=('libasound.so' 'libdbus-1.so' 'libglib-2.0.so' 'libgmodule-2.0.so'
  'libgobject-2.0.so' 'libinstpatch-1.0.so' 'libjack.so' 'libportaudio.so'
  'libpulse-simple.so' 'libreadline.so' 'libsndfile.so' 'libsystemd.so')
  cd "${pkgname}-${pkgver}"
  make -C build DESTDIR="$pkgdir" install
  # systemd user unit
  install -vDm 644 "build/${pkgname}.service" \
    -t "$pkgdir/usr/lib/systemd/user/"
  # system-wide configuration file
  install -vDm 644 "build/${pkgname}.conf" "${pkgdir}/etc/conf.d/${pkgname}"
  # docs
  install -vDm 644 {AUTHORS,ChangeLog,{CONTRIBUTING,README}.md,THANKS,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
