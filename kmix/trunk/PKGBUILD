# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kmix
pkgver=20.12.0
pkgrel=1
pkgdesc="KDE volume control program"
url='https://kde.org/applications/multimedia/kmix/'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(knotifications kxmlgui solid hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools plasma-framework)
groups=(kde-applications kde-multimedia)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('161b743868a4fe3dad42c5a50a9568c61d8e44cf73f1f4f3e53e2de1c8936e72'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
