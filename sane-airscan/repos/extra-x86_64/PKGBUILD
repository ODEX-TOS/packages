# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Thomas Kiss <thokis@gmail.com>

pkgname=sane-airscan
pkgver=0.99.15
pkgrel=1
pkgdesc="SANE - SANE backend for AirScan (eSCL) and WSD document scanners"
url="https://github.com/alexpevzner/sane-airscan"
arch=('x86_64')
license=('GPL2')
depends=('sane' 'libsoup')
#makedepends=('meson')
optdepends=('ipp-usb: allows to send HTTP requests via a USB connection on devices without Ethernet or WiFi connections')
source=("$pkgname-$pkgver.tar.gz"::"https://github.com/alexpevzner/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('fa3f59be56656f3c677cc12afcd83b3a82f87d04025be615225e2b6e55a418e1')

build() {
    cd ${pkgname}-${pkgver}
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README"
    # add missing .so symlink
    cd "${pkgdir}"/usr/lib/sane
    ln -vsf libsane-airscan.so.1 libsane-airscan.so
}
