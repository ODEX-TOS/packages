# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Thomas Kiss <thokis@gmail.com>

pkgname=sane-airscan
pkgver=0.99.21
pkgrel=1
pkgdesc="SANE - SANE backend for AirScan (eSCL) and WSD document scanners"
url="https://github.com/alexpevzner/sane-airscan"
arch=('x86_64')
license=('GPL2')
depends=('sane' 'avahi' 'gnutls' 'libxml2' 'libjpeg-turbo' 'libpng')
#makedepends=('meson')
optdepends=('ipp-usb: allows to send HTTP requests via a USB connection on devices without Ethernet or WiFi connections')
backup=(etc/sane.d/airscan.conf)
source=("$pkgname-$pkgver.tar.gz"::"https://github.com/alexpevzner/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('0f6fcf8425b23909e732016492bd7162f54e447b3871329f9fd13b5046bd8f61')

build() {
    cd ${pkgname}-${pkgver}
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README"
    # add missing .so symlink
    cd "${pkgdir}"/usr/lib/sane
    ln -vsf libsane-airscan.so.1 libsane-airscan.so
}
