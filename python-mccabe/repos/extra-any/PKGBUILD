# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Karol "Kenji Takahashi" Wo≈∫niak <kenji.sx>

pkgbase=python-mccabe
pkgname=(python-mccabe python2-mccabe)
pkgver=0.6.1
pkgrel=7
pkgdesc="McCabe complexity checker for Python"
arch=('any')
url="https://github.com/flintwork/mccabe"
license=('custom:Expat')
makedepends=('python-setuptools' 'python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/flintwork/mccabe/archive/$pkgver.tar.gz")
sha512sums=('1da239933f36c8b7c0f9f026822d673d0b207ac49e45027d77500001327964dd66cc7074be22311927176fc313174f4eb7908d0fc45da9fc4623bf1e237293b0')

prepare() {
  cp -a mccabe-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/mccabe-$pkgver
  python setup.py build

  cd "$srcdir"/mccabe-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/mccabe-$pkgver
  python setup.py test

  cd "$srcdir"/mccabe-$pkgver-py2
  python2 setup.py test
}

package_python-mccabe() {
  depends=('python')

  cd mccabe-$pkgver
  python setup.py install --root="$pkgdir" --prefix=/usr --skip-build --optimize=1

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-mccabe() {
  depends=('python2')

  cd mccabe-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --prefix=/usr --skip-build --optimize=1

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
