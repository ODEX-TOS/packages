# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=pimcommon
pkgver=20.07.80
pkgrel=1
pkgdesc="Common libraries for KDE PIM"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(libkdepim kpimtextedit knewstuff kimap kldap akonadi-contacts akonadi-search purpose)
makedepends=(extra-cmake-modules boost qt5-tools)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('c92d8800695acd1ee454e7a6b89ed3e4c4e99c01162c8525d3ca5794b292d989'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
