# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-usage
pkgver=3.33.2
pkgrel=2
pkgdesc="GNOME application to view information about use of system resources"
arch=(x86_64)
url="https://wiki.gnome.org/Apps/Usage"
license=(GPL3)
depends=(gtk3 libgtop libdazzle tracker)
makedepends=(git meson vala)
optdepends=('accountsservice: show user tags for processes')
groups=(gnome-extra)
_commit=7554b8553d4e29bb771fd72fc4f3d2c7a4b87f02  # tags/v3.33.2^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-usage.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
