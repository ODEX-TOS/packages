# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libksieve
pkgver=20.07.80
pkgrel=1
pkgdesc="KDE PIM library for managing sieves"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(kidentitymanagement pimcommon kmailtransport)
makedepends=(extra-cmake-modules boost kdoctools)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('a54ad4931a6c3d8ebef6cf0250d02cb8f593b633afe6cb78c39c88b3f9fc84ee'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
