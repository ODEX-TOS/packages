# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=rinutils
pkgver=0.4.1
pkgrel=1
pkgdesc="C11 / gnu11 utilities C library"
arch=(any)
url="https://www.shlomifish.org/open-source/projects/"
license=(MIT)
depends=()
makedepends=(cmake)
source=(https://github.com/shlomif/rinutils/releases/download/$pkgver/$pkgname-$pkgver.tar.xz)
sha256sums=('9744fc52f91b817648958b51fca3e331550c6c240308df76b84f512d84a65ef5')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_TEST_SUITE=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 "$srcdir"/$pkgname-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
