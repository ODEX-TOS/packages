# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=python-pylint
pkgver=2.5.3
pkgrel=2
pkgdesc="Analyzes Python code looking for bugs and signs of poor quality"
arch=('any')
url="https://pylint.org"
license=('GPL')
depends=('python-astroid' 'python-mccabe' 'python-isort' 'python-setuptools' 'python-toml')
makedepends=('python-pytest-runner' 'python-pytest-benchmark')
checkdepends=('mpdecimal')
optdepends=('tk: Pylint GUI'
            'graphviz: To have other output formats than dot or vcg')
source=("$pkgname-$pkgver.tar.gz::https://github.com/PyCQA/pylint/archive/pylint-$pkgver.tar.gz"
        pylint-isort-5.patch)
sha512sums=('199ea6b1346fde7b22e9a94d53c7a4832e7bf8a05111d9ec06d50aefad58ad23181d8fd553b682c9c0bc8f96d9775c10835c1a8627c3b46510fbf4292babe042'
            '7cf6b01e82caee761b30cdf12c08a2ed427a261223ed3f740a2d7504352755cf0ed034d7b944a70858bd87e5d74fcd4a78ca59add1b879ae000eee6cf23492b0')

prepare() {
  patch -d pylint-pylint-$pkgver -p1 < pylint-isort-5.patch
}

build() {
  cd pylint-pylint-$pkgver
  python setup.py build
}

check() {
  cd pylint-pylint-$pkgver
  python setup.py pytest
}

package() {
  cd pylint-pylint-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1

  install -d "$pkgdir"/usr/share/man/man1
  install -m644 man/* "$pkgdir"/usr/share/man/man1
}
