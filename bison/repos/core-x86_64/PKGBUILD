# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>

pkgname=bison
pkgver=3.6.3
pkgrel=1
pkgdesc="The GNU general-purpose parser generator"
arch=('x86_64')
license=('GPL3')
url="https://www.gnu.org/software/bison/bison.html"
depends=('glibc' 'm4' 'sh')
groups=('base-devel')
source=("https://ftp.gnu.org/gnu/bison/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha512sums=('8484fcc36f9767b0b5a90ca05e8cd956d38d7c8321038a3ce8b6c2dbf16852d816a03fe09025ad01072d64a2e46a2b42c803313a42e4e3dcc8d4ca6c7f960272'
            'SKIP')

validpgpkeys=('7DF84374B1EE1F9764BBE25D0DDCAA3278D5264E')  # Akim Demaille

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --datadir=/usr/share
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
