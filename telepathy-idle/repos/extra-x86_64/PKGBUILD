# Maintainer:
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bjorn Lindeijer <bjorn lindeijer nl>
# Contributor: Samuel Mendes <heka.lok@gmail.com>

pkgname=telepathy-idle
pkgver=0.2.0
pkgrel=4
pkgdesc="An IRC connection manager for Telepathy"
arch=('x86_64')
url="https://telepathy.freedesktop.org"
groups=('telepathy')
license=('LGPL')
depends=('telepathy-glib')
makedepends=('libxslt' 'python')
source=(https://telepathy.freedesktop.org/releases/$pkgname/$pkgname-$pkgver.tar.gz{,.asc}
        0001-tools-Fix-errors-running-glib-ginterface-gen-under-P.patch
        0002-tools-Remove-outdated-is-unicode-checks.patch)
sha256sums=('3013ad4b38d14ee630b8cc8ada5e95ccaa849b9a6fe15d2eaf6d0717d76f2fab'
            'SKIP'
            '619395db2e0757040f5c067487e748d07b427e67b95a9a8f554255425a631af3'
            '6f3aeded2ec498afd3ced99c242297f3fb0e6e268faf03e3b362a90bf899f20d')
validpgpkeys=('1B5A1BA811AAF0F12169E28AAC558671711E31B1') # Guillaume Desmottes <gdesmott@gnome.org>

prepare() {
    cd $pkgname-$pkgver
    # python3 fixes taken from Fedora
    patch -Np1 -i ../0001-tools-Fix-errors-running-glib-ginterface-gen-under-P.patch
    patch -Np1 -i ../0002-tools-Remove-outdated-is-unicode-checks.patch
}

build() {
    cd $pkgname-$pkgver
    ./configure --prefix=/usr \
    	--libexecdir=/usr/lib/telepathy
    make
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install
}
