# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=pyqtpurchasing
pkgname=(python-pyqtpurchasing)
pkgver=5.15.1
pkgrel=1
pkgdesc="Python bindings for QtPurchasing"
arch=(x86_64)
url="https://www.riverbankcomputing.com/software/pyqtpurchasing/intro"
license=(GPL3)
groups=(pyqt5)
depends=(qt5-purchasing python-pyqt5)
makedepends=(sip5 pyqt-builder)
source=("https://pypi.python.org/packages/source/P/PyQtPurchasing/PyQtPurchasing-$pkgver.tar.gz")
sha256sums=('31d1a1c6a33e5c5269be8e437f3965b8e08387048b8909f9b84a58e8e91235e3')

build() {
  cd "$srcdir"/PyQtPurchasing-$pkgver
  sip-build \
    --no-make \
    --api-dir /usr/share/qt/qsci/api/python
  cd build
  make
}

package_python-pyqtpurchasing(){
  cd PyQtPurchasing-$pkgver/build
  make INSTALL_ROOT="$pkgdir" install
}
