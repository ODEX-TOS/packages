# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=spectacle
pkgver=20.07.80
pkgrel=1
pkgdesc="KDE screenshot capture utility"
arch=(x86_64)
url='https://kde.org/applications/utilities/org.kde.spectacle'
license=(GPL)
depends=(xcb-util-cursor libkipi purpose knewstuff kwayland qt5-tools)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('kipi-plugins: export to various online services')
groups=(kde-applications kde-graphics)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('4fae360914ba019f28b0616011c6854533ff5ea819de2a3ce36eb323f6151980'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
   -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

