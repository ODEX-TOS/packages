# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Nick B <Shirakawasuna at gmail _dot_com>

pkgname=partitionmanager
pkgver=20.12.0
pkgrel=1
pkgdesc="A KDE utility that allows you to manage disks, partitions, and file systems"
arch=(x86_64)
url="https://kde.org/applications/system/org.kde.partitionmanager"
license=(GPL2)
depends=(kpmcore hicolor-icon-theme kio)
makedepends=(extra-cmake-modules kdoctools)
groups=(kde-applications kde-system)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('77e9edb7fac1f0a1798c0dcf1a422f4dabb710921a9026b4e1e06803ee2be31e'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
