# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=eog
pkgver=3.36.3
pkgrel=1
pkgdesc="Eye of Gnome: An image viewing and cataloging program"
url="https://wiki.gnome.org/Apps/EyeOfGnome"
arch=(x86_64)
license=(GPL)
depends=(gnome-desktop libexif lcms2 exempi libpeas librsvg dconf)
makedepends=(gobject-introspection gtk-doc git meson yelp-tools)
optdepends=('eog-plugins: Additional features')
groups=(gnome)
_commit=c0741db6b21a514ed4d0771766b69716cdf6f6ae  # tags/3.36.3^0
source=("git+https://gitlab.gnome.org/GNOME/eog.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson $pkgname build -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
