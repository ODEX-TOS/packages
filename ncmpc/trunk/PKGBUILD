# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Daniel Bainton <dpb [at] backarrow.org>

pkgname=ncmpc
pkgver=0.39
pkgrel=2
pkgdesc="Fully featured MPD client which runs in a terminal"
url='https://www.musicpd.org/clients/ncmpc/'
arch=('x86_64')
license=('GPL2')
depends=('gcc-libs' 'glibc' 'lirc' 'pcre')
makedepends=('boost' 'libmpdclient' 'meson' 'ncurses' 'python-sphinx')
optdepends=('ruby: for lyrics plugin')
source=("https://www.musicpd.org/download/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha512sums=('bfadc58bf8b66bff08608f561f689a50fd412b87975998ce23751fd312fa009eeba4b48b4afe96391b2ecb184c00c366262e9b96e24d2ce2ab2af6a4002721d9'
            'SKIP')
b2sums=('1b270ed52aa884cecd3e1ce60bb2ed628ef79480697f388b48a09e0f22068dbbb7623b01171e5cfae9556446e404530f93c84ca3c3d5cc481c8431f37786c2f4'
        'SKIP')
validpgpkeys=('0392335A78083894A4301C43236E8A58C6DB4512') # Max Kellermann <max@blarg.de>

build() {
  cd "${pkgname}-${pkgver}"
  meson --prefix /usr \
        --libexecdir lib \
        --sbindir bin \
        --buildtype plain \
        --auto-features enabled \
        --wrap-mode nodownload \
        -D b_lto=true \
        -D b_pie=true \
        -D lyrics_screen=true \
        -D chat_screen=true \
        build
  ninja -C build
}

package() {
  depends+=('libmpdclient.so' 'libncursesw.so')
  cd "${pkgname}-${pkgver}"
  DESTDIR="${pkgdir}" ninja -C build install
}
